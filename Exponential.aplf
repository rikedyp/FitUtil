 abr2←x Exponential y;ab;r2;yt
⍝ FIND THE COEFFICIENTS(a,b) OF AN EXPONENTIAL REGRESSION
⍝ Y' = a × exp(b×X)

 abr2←0 0 0

 :If 2>⍴∪x
     ∆CRLF,'2. EXPONENTIAL:     Not Enough Independant Points for a Regression' ⋄ →0
 :ElseIf ∨/y≤0
     ∆CRLF,'2. EXPONENTIAL:     Regression Not Possible because some Y values are ≤ 0' ⋄ →0
 :End

 :Trap 0
     ab←(⍟y)⌹x∘.*0 1
     ab[1]←*ab[1]
     yt←ab YTExponential x
     yt←⍟yt
     y←⍟y
     r2←1-(+/(y-yt)*2)÷+/(y-(+/y)÷⍴y)*2
     abr2←ab,r2

     ∆CRLF,'2. EXPONENTIAL:     Y'' = ',(⍕abr2[1]),' × exp(',(⍕abr2[2]),'×X)   (r² = ',(DEB 3⍕abr2[3]),')'
 :Else
     ∆CRLF,'2. EXPONENTIAL:     Regression not possible' ⋄ →0
 :EndTrap
