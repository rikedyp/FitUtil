 abcr2←x Parabolic0 y;abc;r2;xy;x1;x2;x3;yt;y1
⍝ FIND THE COEFFICIENTS(b,c) OF A PARABOLIC REGRESSION FORCED THROUGH THE POINT (0,0)
⍝ Y' = 0 + b×X + c×X*2

 abcr2←0 0 0 0

 :If 2>⍴∪x
     ∆CRLF,'8. PARABOLIC @(0,0): Not Enough Independant Points for a Regression' ⋄ →0
 :End

 :Trap 0
     x1←+/x ⋄ x2←+/x*2 ⋄ x3←+/x*3
     xy←+/x×y ⋄ y1←+/y
     abc←0,(y1,xy)⌹2 2⍴x1,x2,x2,x3
     yt←abc YTParabolic x

   ⍝ r2 may not be valid because the curve is forced through the origin
     r2←1⌊0⌈1-(+/(y-yt)*2)÷+/(y-(+/y)÷⍴y)*2
     abcr2←abc,r2

     ∆CRLF,'8. PARABOLIC @(0,0): Y'' = 0 + ',(⍕abcr2[2]),'×X + ',(⍕abcr2[3]),'×X*2   (r² = ',(DEB 3⍕abcr2[4]),')'
 :Else
     ∆CRLF,'8. PARABOLIC @(0,0): Regression not possible' ⋄ →0
 :EndTrap
