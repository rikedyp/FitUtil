 x FIT y;line;RegName;temp
⍝ Program to Perform 10 Different Regressions.
⍝
⍝ Use TFIT to Perform the Regressions with a Student Test
⍝ that will remove the non significative points.
⍝
⍝ ∆FIT = Global Nested Array of Regression Coefficients used by SHOW
⍝
⍝ If 'regno' is the regression number (1 to 10), you can obtain
⍝ it's regression coefficients by doing: ¯1↓regno 1⊃∆FIT


 (⎕IO ⎕ML ⎕WX ⎕PP)←1 3 3 5 ⍝ System variables
 ∆CRLF←⎕UCS 13 10

 x←,x ⋄ y←,y           ⍝ To make sure x and y are vectors.
 line←90⍴'-'
 RegName←⊃'LINEAR' 'EXPONENTIAL' 'POWER' 'LOGARITHMIC' 'PARABOLIC' 'CUBIC' 'LINEAR(0,0)' 'PARABOLIC(0,0)' 'CUBIC(0,0)' 'WEIBULL'
 ∆FIT←(10⍴⊂(0 0 0 0 0)(0 0)),(⊂0 0),(⊂0 0)
 temp←(⍴x)⍴0 ⍝ Filler to Indicate that No Points were Removed. Used by TFIT.

⍝ If x and y are not the same dimension we exit.
 :If (⍴x)≠⍴y
     'X and Y Don''t Have the Same Number of Points.',∆CRLF,'Correct it and Try Again' ⋄ →0
 :End

⍝ We need at least 2 Independants Points
 :If 2>⍴∪x
     'Not Enough Independants Points to Perform a Regression' ⋄ →0
 :End

 ∆CRLF,line
 'CALCULATION OF THE CORRELATION''S COEFFICIENTS'
 line

 ∆FIT←⊂(x Linear y)temp
 ∆FIT←∆FIT,⊂(x Exponential y)temp
 ∆FIT←∆FIT,⊂(x Power y)temp
 ∆FIT←∆FIT,⊂(x Logarithmic y)temp
 ∆FIT←∆FIT,⊂(x Parabolic y)temp
 ∆FIT←∆FIT,⊂(x Cubic y)temp
 ∆FIT←∆FIT,⊂(x Linear0 y)temp
 ∆FIT←∆FIT,⊂(x Parabolic0 y)temp
 ∆FIT←∆FIT,⊂(x Cubic0 y)temp
 ∆FIT←∆FIT,⊂(x Weibull y)temp
 ∆FIT←∆FIT,(⊂x),⊂y

 ∆CRLF,line
 :If 0≠¯1↑1⊃(temp←1⍴⍒∊¯1↑¨1⊃¨10↑∆FIT)⊃∆FIT
     'THE BEST REGRESSION IS No ',(⍕temp),': ',,RegName[temp;]
 :Else
     'NO REGRESSION POSSIBLE'
 :End
 line,∆CRLF

 #.WPF.SHOW ∆FIT
