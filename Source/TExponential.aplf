 r←abr2 TExponential(x y);ab;dof;fn;sty;sy2;test;xave;yt
⍝ REMOVE THE NON-SIGNIFICATIVES POINTS OF AN EXPONENTIAL REGRESSION USING A STUDENT TEST

 dof←2 ⋄ ab←abr2[1 2] ⋄ r←abr2((⍴x)⍴0)

 :If 0≥fn←(⍴∪x)-dof
     ∆CRLF,'2. EXPONENTIAL:     Not Enough Independant Points to Perform the Test' ⋄ →0
 :ElseIf 0 0≡ab
     ∆CRLF,'2. EXPONENTIAL:     Regression Not possible' ⋄ →0
 :End

 yt←ab YTExponential x
 xave←(+/x)÷⍴x
 sy2←((+/(y-yt)*2)÷fn)×((÷fn+dof)+((x-xave)*2)÷+/(x-xave)*2)
 sty←(sy2*0.5)×∆Tstudent[30⌊fn]
 test←sty≥|y-yt
 x←test/x ⋄ y←test/y

 :If 0≥fn←(⍴y)-dof
     ∆CRLF,'2. EXPONENTIAL:     Not Enough Independant Points to Perform the Test' ⋄ →0
 :ElseIf 0 0 0≡abr2←x Exponential y
     ∆CRLF,'2. EXPONENTIAL:     Regression Not possible' ⋄ →0
 :End

 :If 0=+/~test
     '   No Points Removed'
 :Else
     '   The Following Points were Removed: ',⍕(~test)/⍳⍴test
 :End

 r←abr2(~test)
