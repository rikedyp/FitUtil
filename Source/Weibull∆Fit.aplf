 Weibull∆Fit b;AT;C1;C2;D;EPS;EPS2;g;G;H;IM;INC;IT;LOW;MM;PIE;RM;SS;UPPER
⍝ CALCULATES NONLINEAR LEAST SQUARES ESTIMATORS. USES Weibull∆Perturb

 BSAVE←b←,b

⍝THIS SETS UP THE CONSTANTS THAT GUIDE THE RATE OF CONVERGENCE
 C1←PIE←0.2 ⋄ C2←5 ⋄ UPPER←1000 ⋄ EPS←0.00001 ⋄ EPS2←0.0001 ⋄ INC←0.01 ⋄ IT←49

 ASAVE←AT←,FUNC b
 OLDSS←SS←+/AT*2
 Weibull∆Perturb b
 LOW←COUNT←0

LOOP:→(IT<COUNT←COUNT+1)/0

⍝ MARQUARDT
 IM←(⍴m)⍴1,((RM←(⍴m)[1]))⍴0
 MM←(C1×IM)+m÷D∘.×D←((1 1)⍉m)*0.5
 H←((g÷D)⌹MM)÷D

 →((C1≥UPPER)∨(EPS∧.≥(|H)÷EPS2+|b))/0
 b←b+H

GOOD:AT←,FUNC b
 SS←+/AT*2
 →(EPS2>|(OLDSS-SS)÷OLDSS)/0
 →(SS≤OLDSS)/FINE
 C1←C1×C2
 b←b-H
 →LOOP

FINE:OLDSS←SS
 ASAVE←AT
 BSAVE←b
 LOW←COUNT
 C1←C1÷C2
 Weibull∆Perturb b
 →LOOP
