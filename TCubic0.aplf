 r←abcdr2 TCubic0(x y);abcd;dof;fn;sty;sy2;test;xave;yt
⍝ REMOVE THE NON-SIGNIFICATIVES POINTS OF A CUBIC REGRESSION
⍝ FORCED THROUGH THE ORIGIN USING A STUDENT TEST

 dof←3 ⋄ abcd←abcdr2[1 2 3 4] ⋄ r←abcdr2((⍴x)⍴0)

 :If 0≥fn←(⍴∪x)-dof
     ∆CRLF,'9. CUBIC @(0,0):     Not Enough Independant Points to Perform the Test' ⋄ →0
 :ElseIf 0 0 0 0≡abcd
     ∆CRLF,'9. CUBIC @(0,0):     Regression Not possible' ⋄ →0
 :End

 yt←abcd YTCubic x
 xave←(+/x)÷⍴x
 sy2←((+/(y-yt)*2)÷fn)×((÷fn+dof)+((x-xave)*2)÷+/(x-xave)*2)
 sty←(sy2*0.5)×∆Tstudent[30⌊fn]
 test←sty≥|y-yt
 x←test/x ⋄ y←test/y

 :If 0≥fn←(⍴∪x)-dof
     ∆CRLF,'9. CUBIC @(0,0):     Not Enough Independant Points to Perform the Test' ⋄ →0
 :ElseIf 0 0 0 0 0≡abcdr2←x Cubic0 y
     ∆CRLF,'9. CUBIC @(0,0):     Regression Not possible' ⋄ →0
 :End

 :If 0=+/~test
     '   No Points Removed'
 :Else
     '   The Following Points were Removed: ',⍕(~test)/⍳⍴test
 :End

 r←abcdr2(~test)
